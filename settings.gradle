import org.gradle.util.GradleVersion
import org.gradle.api.GradleScriptException

// Minimum Gradle version for main build
def minGradleVersion = GradleVersion.version("5.6")
// Minimum Gradle version for builds of JavaFX 11 module
def minFxGradleVersion = GradleVersion.version("5.6")

rootProject.name = 'dashj-parent'

if (GradleVersion.current().compareTo(minGradleVersion) < 0) {
    throw new GradleScriptException("dashj build requires Gradle ${minGradleVersion} or later", null)
}

include 'core'
project(':core').name = 'dashj-core'

include 'tools'
project(':tools').name = 'dashj-tools'

include 'examples'
project(':examples').name = 'dashj-examples'

if (GradleVersion.current().compareTo(minFxGradleVersion) >= 0 && JavaVersion.current().isJava11Compatible()) {
    System.err.println "Including wallettemplate because ${GradleVersion.current()} and Java ${JavaVersion.current()}"
    include 'wallettemplate'
    project(':wallettemplate').name = 'dashj-wallettemplate'
} else {
    System.err.println "Skipping wallettemplate, requires ${minFxGradleVersion}+ and Java 11+, currently running: ${GradleVersion.current()} and Java ${JavaVersion.current()}"
}